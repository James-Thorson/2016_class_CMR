model{
  for(i in 1:N) {
    Abun[i]~dpois(mu[i])
    log(mu[i])<-alpha +
                Month1[i]*b[1] + Month2[i]*b[2]+
                TDay1[i]*b[3] + TDay2[i]*b[4]+
                Month1[i]*TDay1[i]*b[5]+
                W[Wind2[i]]+S[Site[i]]+eps[i]
    eps[i] ~ dnorm(0.0, eps.tau)
  }
  for(i in 1:N) {
    Aprd[i] ~ dpois(mu[i])
    e.obs[i] <- (Abun[i] - mu[i]) / sqrt(mu[i]) 
    p2.obs[i] <- e.obs[i] * e.obs[i]
    e.prd[i] <- (Aprd[i] - mu[i]) / sqrt(mu[i]) 
    p2.prd[i] <- e.prd[i] * e.prd[i]
  }
  SS     <- sum(p2.obs[1:N])
  SS.prd <- sum(p2.prd[1:N])
  for(k in 1:3) {
    for (t in k+1 : N) { 
      p1[k,t] <- e.obs[t] * e.obs[t - k]      
    }
    auto [1, k] <- sum(p1[k, (N11 + k) : N12]) / sum(p2.obs[N11: N12])
    auto2[1, k] <- auto[1,k]*auto[1,k]
#   auto2[1, k] <- auto[1, k]/(1-auto[1, k] * auto[1, k])
    auto [2, k] <- sum(p1[k, (N21 + k) : N22]) / sum(p2.obs[N21 : N22])
    auto2[2, k] <- auto[2, k] * auto[2, k]
#   auto2[2, k] <- auto[2, k]/(1-auto[2, k] * auto[2, k])
  }
  BP.s1 <- (N12 - N11 - 1) * sum(auto2[1, ])
  BP.s2 <- (N22 - N21 - 1) * sum(auto2[2, ])

  alpha~dnorm(0,1.0E-6)
  for(j in 1:5) {
    b[j]~dnorm(0.0,1.0E-6)
  }
  for (i in 2:4){
    W[i]~dnorm(0.0,1.0E-6)
  }
  W[1]<-0
  S[1]<-0
  S[2]~dnorm(0.0,1.0E-6)
  eps.tau ~ dgamma(0.001, 0.001)
  eps.sigma<-1/eps.tau
}

  
  

